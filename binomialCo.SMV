

MODULE ALG1(b,y1,y2,n,k)

	VAR
		state : {l0, l1, l2, l3};
	ASSIGN
		init(state) := l0;
		next(b) :=
			case
				state = l1 & (b * y1) <500 : b * y1;
				TRUE : b;
			esac;
		next(y1) :=
			case
				state = l2 & y1 >0: y1 - 1;
				TRUE : y1;
			esac;
		next(state) :=
			case
				state = l0 & (y1 > (n - k)) : l1;
				state = l0 & (y1 <= (n - k)): l3;
				state = l1 : l2;
				state = l2 : l0;
				TRUE : state;
			esac;
FAIRNESS
	running



MODULE ALG2(b,y1,y2,n,k)
	VAR
		state : {m0,m1,m2,m3,m4};
	
	ASSIGN
		init(state) := m0;
		next(b) :=
			case
				state = m2 : b / y2;
				TRUE : b;
			esac;
		next(y2) :=
			case
				state = m3 & (y2 < 15): y2 + 1;
				TRUE : y2;
			esac;
		next(state) :=
			case
				state = m0 & (y2 > k) : m4;
				state = m0 & (y2 <= k) : m1;
				state = m1 & ((y1 + y2) <= n) : m2;
				state = m2 : m3;
				state = m3 : m0;
				TRUE : state;
			esac;


FAIRNESS
	running
	
MODULE main
	VAR	
		n  : 1..15;
		k  : 1..15;
		b  : 0..500;
		y2 : 1..16;
		y1 : 0..15;
		proc1 : process ALG1(b,y1,y2,n,k);
		proc2 : process ALG2(b,y1,y2,n,k);
	ASSIGN
		init(b) := 1;
		init(y1) := n;
		init(y2) := 1;


SPEC AG (proc1.state = l0 -> EF proc1.state = l3)
SPEC AG (proc2.state = m0 -> EF proc2.state = m4)
SPEC AG ((proc1.state = l0 -> EF proc1.state = l3) & (proc2.state = m0 -> EF proc2.state = m4))


SPEC EF (proc1.state = l0 & EF proc2.state = m0)
SPEC EF (proc1.state = l1 & EF proc2.state = m1)
SPEC EF (proc1.state = l2 & EF proc2.state = m2)
SPEC EF (proc1.state = l3 & EF proc2.state = m3)

SPEC EF (proc1.state = l0 & EF proc2.state = m4)


SPEC AF ((n = 3 & k = 2) -> EF proc2.state = m4 -> EF proc1.state = l3 & b = 3)
SPEC AF ((n = 3 & k = 2) -> EF proc1.state = l3 -> EF b = 3)










